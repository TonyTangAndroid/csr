apply plugin: 'com.android.library'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    minSdkVersion 17
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

repositories {
  maven {
    url "https://s3.amazonaws.com/repo.commonsware.com"
  }
}

dependencies {
  debugCompile project(':netsecurity')
  releaseCompile 'com.commonsware.cwac:netsecurity:0.4.4'
  compile 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
  compile 'info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1'
  compile 'com.squareup.okhttp3:okhttp:3.8.0'
}

if (project.hasProperty('PUBLISH_GROUP_ID')) {
  // from http://stackoverflow.com/a/19484146/115145

  android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
      return; // Skip debug builds.
    }
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.baseName = "cwac-netsecurity-netcipher"
    task.version = PUBLISH_VERSION
    task.exclude('com/commonsware/cwac/**/BuildConfig.**')
  }

  // based on http://stackoverflow.com/a/23672732/115145

  android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
      description "Generates Javadoc for $variant.name."
      source = variant.javaCompile.source
      ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
      classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
      exclude '**/BuildConfig.java'
      exclude '**/R.java'
      exclude '**/config/*.java'
      exclude '**/conscrypt/*.java'
      exclude '**/internalutil/*.java'
      exclude '**/luni/*.java'
    }
  }

  task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
  }

  artifacts {
    archives sourcesJar
  }

  apply plugin: 'maven'

  uploadArchives {
    repositories.mavenDeployer {
      pom.groupId = PUBLISH_GROUP_ID
      pom.artifactId = 'netsecurity-netcipher'
      pom.version = PUBLISH_VERSION

      repository(url: LOCAL_REPO)
    }
  }
}
